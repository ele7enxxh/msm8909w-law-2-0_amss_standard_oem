#===============================================================================
#
# DAL SYS Lib
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2009 - 2011 Qualcomm Incorporated.
# All Rights Reserved.
# Qualcomm Confidential and Proprietary
#
#-------------------------------------------------------------------------------
#
#  $Header: //components/rel/core.mpss/3.5.c12.3/dal/sys/build/SConscript#1 $
#  $DateTime: 2016/12/13 07:59:23 $
#  $Author: mplcsds1 $
#  $Change: 11985146 $
#                      EDIT HISTORY FOR FILE
#                      
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#  
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
#
#===============================================================================
import os
Import('env')
env = env.Clone()

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------
SRCPATH = "${DAL_ROOT}/sys/src"
env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0) 

#-------------------------------------------------------------------------------
# Internal depends within CoreBSP
#-------------------------------------------------------------------------------
CBSP_API = [
   'DEBUGTOOLS',
   'SERVICES',
   'MPROC',
   'SYSTEMDRIVERS',   
   # needs to be last also contains wrong comdef.h
   'KERNEL',
]

env.RequirePublicApi(CBSP_API)
env.RequireRestrictedApi(CBSP_API)
env.RequireRestrictedApi('DAL')

#-------------------------------------------------------------------------------
# Define environment
#-------------------------------------------------------------------------------
env.Append(CPPDEFINES = [
   "DALSYS_WL_DEFAULT_PRIO=(166 - MODEM_FW_NUM_PRIO)",
   "DALSYS_WL_DEFAULT_STACK_SIZE=0x2000",
   "DALSYS_LOG_LEVEL=DALSYS_LOGEVENT_WARNING",
   "DALSYS_MEM_BUS_ATTR=1",
   "DALSYS_MEM_64BIT_PHYS_ADDR",
   "DALSYS_SYNC_CEILING_PRIO=MODEM_FW_NUM_PRIO",
   ])

if not env.has_key('USES_MBA'):
   env.Append(CPPDEFINES = [
      "DALSYS_LOG_BUFFER_SIZE_POWER=7",
      "DALSYS_WL_TRACK_CNT=32",
      "DALSYS_SYNC_USE_PIMUTEX",
      ])

if env.PathExists("${BUILD_ROOT}/core/api/debugtools/err.h"):
   env.Append(CPPDEFINES = ["DAL_ERR_LOG"])

#-------------------------------------------------------------------------------
# MBA Source Code
#-------------------------------------------------------------------------------
DALSYS_MBA_SOURCES =  [
   '${BUILDPATH}/DALSysLogEvent.c',
   '${BUILDPATH}/DALSysMem.c',
   '${BUILDPATH}/DALSysMem_static.c',
]

if env.has_key('USES_QPIC_NAND'):
   DALSYS_MBA_SOURCES.extend([
      '${BUILDPATH}/dal_asm_wrap.c',
      '${BUILDPATH}/DALModEnv_guestos.c',
      '${BUILDPATH}/DALSysCmn.c',
      '${BUILDPATH}/DALSysEvent.c',
      '${BUILDPATH}/DALSysSync.c',
      '${BUILDPATH}/DALSysTimer_qtimer.c',
      '${BUILDPATH}/DALSysWorkLoop.c',
      '${BUILDPATH}/DALSysWorkLoop_qthread.c',
      ])

env.AddLibrary(['MBA_CORE_SW'], '${BUILDPATH}/DALSys_mba', DALSYS_MBA_SOURCES)

#-------------------------------------------------------------------------------
# User Source Code
#-------------------------------------------------------------------------------
DALSYS_MPSS_SOURCES =  [
   '${BUILDPATH}/dal_asm_wrap.c',
   '${BUILDPATH}/DALGlbCtxt.c',
   '${BUILDPATH}/DALModEnv_guestos.c',
   '${BUILDPATH}/DALSysCmn.c',
   '${BUILDPATH}/DALSysEvent.c',
   '${BUILDPATH}/DALSysLogEvent.c',
   '${BUILDPATH}/DALSysMem.c',
   '${BUILDPATH}/DALSysMem_dynamic.c',
   '${BUILDPATH}/DALSysSync.c',
   '${BUILDPATH}/DALSysTimer_ats.c',
   '${BUILDPATH}/DALSysWorkLoop.c',
   '${BUILDPATH}/DALSysWorkLoop_rex.c',
]

env.AddLibrary(
   ['SINGLE_IMAGE', 'CBSP_SINGLE_IMAGE', 'MODEM_IMAGE', 'CBSP_MODEM_IMAGE',
    'APPS_IMAGE', 'CBSP_APPS_IMAGE', 'QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE'],
    '${BUILDPATH}/DALSys_mpss', DALSYS_MPSS_SOURCES)

#-------------------------------------------------------------------------------
# Add CMM Scripts
#-------------------------------------------------------------------------------
SCRIPTPATH = env['BUILD_ROOT']+'/core/dal/sys/scripts'
try:
   env.AddCMMScripts ('MPSS', [SCRIPTPATH], { 'DALLog.cmm' : 'DAL Log' }, 'dal')
except:
  pass

#-------------------------------------------------------------------------------
# RC INIT
#-------------------------------------------------------------------------------
if 'USES_RCINIT' in env:
   RCINIT_IMG = ['CORE_MODEM', 'CORE_QDSP6_SW']
   env.AddRCInitFunc(           # Code Fragment in TMC: YES
    RCINIT_IMG,                 # define TMC_RCINIT_INIT_DALSYS_INIT
    {
     'sequence_group'             : 'RCINIT_GROUP_0',                   # required
     'init_name'                  : 'dalsys',                           # required
     'init_function'              : 'DALSYS_Init',                      # required
     'dependencies'               : ['kernel_msa_init',]
    })
