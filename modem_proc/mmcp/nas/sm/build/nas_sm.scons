#===============================================================================
#
# NAS SM SCons 
#
# GENERAL DESCRIPTION
#    SCons build script
#
# Copyright (c) 2010 - 2011 by Qualcomm Technologies, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header: //commercial/MPSS.JO.2.0.c1.4/Main/modem_proc/mmcp/nas/sm/build/nas_sm.scons#1 $
#  $DateTime: 2016/12/13 08:00:05 $
#
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
# 04/28/11   pm      Update for MSGR_DEFINE_UMID() calls
# 04/20/11   pm      Update for New Mode Handler in 8660 SA
# 04/12/11   pm      Set MSG_BT_SSID_DFLT to map legacy messages to your SSID name
# 09/15/10   pm      Initial file
#
#===============================================================================
#from glob import glob
#from os.path import join, basename

Import('env')
env = env.Clone()

if 'USES_GSM' not in env and ('USES_UMTS' not in env or 'USES_WPLT' in env):
   Return()

# --------------------------------------------------------------------------- #
# Turn off/on debug if requested 			      
# --------------------------------------------------------------------------- # 
if ARGUMENTS.get('DEBUG_OFF','no') == 'yes':
    env.Replace(ARM_DBG = "")
    env.Replace(HEXAGON_DBG = "")
    env.Replace(GCC_DBG = "")

if ARGUMENTS.get('DEBUG_ON','no') == 'yes':
    env.Replace(ARM_DBG = "-g --dwarf2") 
    env.Replace(HEXAGON_DBG = "-g")  
    env.Replace(GCC_DBG = "-g")

#-------------------------------------------------------------------------------
# Setup source PATH
#-------------------------------------------------------------------------------
SRCPATH = '../src'

env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# Set MSG_BT_SSID_DFLT for legacy MSG macros
#-------------------------------------------------------------------------------
env.Append(CPPDEFINES = [
    'MSG_BT_SSID_DFLT=MSG_SSID_NAS_SM',
])

#-------------------------------------------------------------------------------
# For MSGR_DEFINE_UMID() calls
#-------------------------------------------------------------------------------
if 'USES_MSGR' in env:
    env.AddUMID('${BUILDPATH}/esm_cm_msgr_enum.umid', ['../../../api/esm_cm_msgr_enum_msg.h'])
    env.AddUMID('${BUILDPATH}/esm_emm_msgr_enum.umid', ['../inc/esm_emm_msgr_enum_msg.h'])
    env.AddUMID('${BUILDPATH}/sm_cm_msgr_enum.umid', ['../../../api/sm_cm_msgr_enum_msg.h'])

#-------------------------------------------------------------------------------
# Publish our Private API
#-------------------------------------------------------------------------------
env.PublishPrivateApi('NAS', ["${INC_ROOT}/mmcp/nas/sm/inc"])

#-------------------------------------------------------------------------------
# Necessary Public API's
#-------------------------------------------------------------------------------
REQUIRED_PUBLIC_APIS = [
    'DATA',
    'DATACOMMON',
    'GERAN',
    'GPS',
    'LTE',
    'MCS',
    'MMODE',
    'NAS',
    'ONEX',
    'UIM',
    'UTILS',
    'WCDMA',
    ]

REQUIRED_RESTRICTED_APIS = [
    'DATA',
    'DATACOMMON',
    'FW',
    'GERAN',
    'GPS',
    'LTE',
    'MCS',
    'MDSP',
    'MMODE',
    'NAS',
    'UIM',
    'UTILS',
    'WCDMA',
    ]

CORE_APIS = [
    'DAL',
    'DEBUGTOOLS',
    'MPROC',
    'SERVICES',
    'STORAGE',
    'SYSTEMDRIVERS',

    # needs to be last also contains wrong comdef.h
    'KERNEL',
    ]

MULTIMEDIA_APIS = [
    'MVS',
    ]

env.RequirePublicApi(CORE_APIS, area='core')

env.RequirePublicApi('AUDIO', area='MULTIMEDIA') 
env.RequirePublicApi(MULTIMEDIA_APIS, area='MULTIMEDIA')

env.RequirePublicApi(REQUIRED_PUBLIC_APIS) 
env.RequireRestrictedApi(REQUIRED_RESTRICTED_APIS)

#-------------------------------------------------------------------------------
# External API's not built with SCons
#-------------------------------------------------------------------------------
env.RequireExternalApi([
    'BREW',
    'MULTIMEDIA_AUDIO',
])

#-------------------------------------------------------------------------------
# Generate the library and add to an image
#-------------------------------------------------------------------------------

# Construct the list of source files by looking for *.c
#SM_C_SOURCES = ['${BUILDPATH}/' + basename(fname)
#                 for fname in glob(join(env.subst(SRCPATH), '*.c'))]

SM_C_SOURCES = [
	'${BUILDPATH}/smcm.c',
	'${BUILDPATH}/smgmm.c',
	'${BUILDPATH}/smix.c',
	'${BUILDPATH}/smrabm.c',
	'${BUILDPATH}/smsndcp.c',
	'${BUILDPATH}/smtask.c',
	'${BUILDPATH}/smtimers.c',
	'${BUILDPATH}/smutils.c',    
]

SM_LTE_SOURCES = [
	'${BUILDPATH}/esm_bcm.c',
	'${BUILDPATH}/esm_bpm.c',
	'${BUILDPATH}/esm_context_active.c',
	'${BUILDPATH}/esm_context_active_pedning.c',
	'${BUILDPATH}/esm_context_modify_pending.c',
	'${BUILDPATH}/esm_context_inactive.c',
	'${BUILDPATH}/esm_context_state.c',
	'${BUILDPATH}/esm_onex_csfb.c',
	'${BUILDPATH}/esm_proc_abort.c',
	'${BUILDPATH}/esm_proc_active.c',
	'${BUILDPATH}/esm_proc_inactive.c',
	'${BUILDPATH}/esm_proc_state.c',
	'${BUILDPATH}/esm_utils.c',
	'${BUILDPATH}/sm_multimode_handler.c',
	'${BUILDPATH}/esmtask.c',
]

if 'USES_LTE' in env: 
    SM_C_SOURCES += SM_LTE_SOURCES


# Add our library to the ModemApps image
env.AddLibrary( ['MODEM_MODEM'], '${BUILDPATH}/nas_sm', [SM_C_SOURCES] )


# Build image for which this task belongs
RCINIT_SM = ['MODEM_MODEM']

# RC Init Function Dictionary
RCINIT_INIT_SM = {
            'sequence_group'      : env.subst('$MODEM_UPPERLAYER'),
            'init_name'           : 'sm_init',
            'init_function'       : 'sm_init_before_task_start',
            'policy_optin'        : ['default', 'ftm', ],
    }

# RC Init Function Dictionary
RCINIT_INIT_RABM = {
            'sequence_group'      : env.subst('$MODEM_UPPERLAYER'),
            'init_name'           : 'rabm_init',
            'init_function'       : 'rabm_init_before_task_start',
            'policy_optin'        : ['default', 'ftm', ],
    }

# RC Init Task Dictionary
RCINIT_TASK_SM = {
            'thread_name'         : 'sm',
            'sequence_group'      : env.subst('$MODEM_UPPERLAYER'),
            'stack_size_bytes'    : env.subst('$SM_STKSZ'),
            'priority_amss_order' : 'SM_PRI_ORDER',
            'stack_name'          : 'sm_stack',
            'thread_entry'        : 'sm_task',
            'tcb_name'            : 'sm_tcb',
            'cpu_affinity'        : env.subst('$MODEM_NAS_CPU_AFFINITY'),
            'policy_optin'        : ['default', 'ftm', ],
    }
    
# Add init function to RCInit
if 'USES_MODEM_RCINIT' in env:
            env.AddRCInitFunc(RCINIT_SM, RCINIT_INIT_SM)
            env.AddRCInitFunc(RCINIT_SM, RCINIT_INIT_RABM)
            env.AddRCInitTask(RCINIT_SM, RCINIT_TASK_SM)


